plugins {
    id 'org.jetbrains.intellij' version '1.7.0'
    id "org.jetbrains.grammarkit" version "2021.2.2"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

apply plugin: 'org.jetbrains.grammarkit'

sourceSets.main.java.srcDirs 'src/main/gen'

group 'org.swim'
version '1.3.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2022.1.3'
    plugins = ['PsiViewer:221-SNAPSHOT', 'com.intellij.java']
    updateSinceUntilBuild = false
}

patchPluginXml {
    sinceBuild = '203'
}

test {
    systemProperty "idea.home.path", System.getProperty("user.dir")
}

import org.jetbrains.grammarkit.tasks.*

task generateAll {
    group = "generate"
    dependsOn 'generateReconlLexer'
    dependsOn 'generateReconParser'
}

task generateReconlLexer(type: GenerateLexerTask) {
    group = "generate"
    source = "src/main/java/org/swim/language/Recon.flex"
    targetDir = "src/main/gen/org/swim/language"
    targetClass = "ReconLexer"
    purgeOldFiles = true
}

task generateReconParser(type: GenerateParserTask) {
    group = "generate"
    source = "src/main/java/org/swim/language/Recon.bnf"
    targetRoot = 'src/main/gen/'
    pathToParser = 'org/swim/language/parser/ReconParser.java'
    pathToPsiRoot = '/org/swim/language/psi'
    purgeOldFiles = true
}