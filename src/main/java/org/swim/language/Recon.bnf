{
  parserClass="org.swim.language.parser.ReconParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Recon"
  psiImplClassSuffix="Impl"
  psiPackage="org.swim.language.psi"
  psiImplPackage="org.swim.language.psi.impl"

  elementTypeHolderClass="org.swim.language.psi.ReconTypes"
  elementTypeClass="org.swim.language.psi.ReconElementType"
  tokenTypeClass="org.swim.language.psi.ReconTokenType"
}

reconFile ::= (slots | attribute | blockItem | literal | COMMENT | whiteSpace)*

blockItem ::= literal SP* (attribute whiteSpace* blockItem?)? | attribute whiteSpace* ((attribute | primitive) whiteSpace*)* blockItem? | COMMENT

primitive ::= DATA | IDENT | STRING | NUM | BOOL | HEX

literal ::= record | primitive

attribute ::= ATTR whiteSpace* (OPEN_BRACK attr_block? CLOSE_BRACK)?

attr_block ::= whiteSpace* attr_slots whiteSpace*

attr_slots ::= (empty_slot | slot) SP* (((whiteSpace* SEP) | NL) whiteSpace* attr_slots?)?

slots ::= (empty_slot | slot) SP* (((whiteSpace* SEP) | NL) whiteSpace* slots?)?

slot ::= blockItem (whiteSpace* COL whiteSpace* blockItem)?

empty_slot ::= blockItem whiteSpace* COL SP* (NL | SEP) whiteSpace* slots?

block ::= whiteSpace* slots whiteSpace*

record ::= OPEN_CRL_BRACK whiteSpace* block? whiteSpace* CLOSE_CRL_BRACK

whiteSpace ::= NL | SP